


PcmBuf:
        align $100          ;
        DS 256              ;

TL:
        align $100          ;
OP1:    DS 256              ;
OP2:    DS 256              ;



EnterPcmMode:
        PUSH AF             ;11
        
        LD A,$0D            ;Execute
        CALL $37CC          ;PutCommand
        LD A,PcmMode>>8     ;SubSystem
        CALL $37D2          ;PutData
        LD A,PcmMode&$FF    ;SubSystem
        CALL $37D2          ;PutData
        
@:      
        IN A,($FE)          ;11
        AND $0F             ;7
        CP %00000111        ;4  Sync.Signal.on
        JR NZ,@B            ;7|12
        
        LD A,1<<5           ;7  Cmd.Nop
        OUT ($FE),A         ;11
@:      
        IN A,($FE)          ;11
        AND $0F             ;7  Sync.Signal.off
        JR NZ,@B            ;7|12
        
        POP AF              ;10
        RET                 ;10



Pcm1ch:
        EX AF,AF'           ;4
        EXX                 ;4
        
        LD A,$FF            ;7
        OUT ($E4),A         ;11
        
        LD A,$27            ;7
        OUT ($44),A         ;11
        JP @F               ;10
@:      LD A,%00010101      ;7
        OUT ($45),A         ;11
        ;72
        
        
PcmAdr: LD HL,0             ;10
        LD A,(HL)           ;7
        OR A                ;4
        JR Z,@F             ;7|12
        INC HL              ;6
        LD (PcmAdr+1),HL    ;16
@:      ;50
        
        LD H,TL>>8          ;7
        LD L,A              ;4
        ;11
        
        LD A,$40            ;7  OP1.TL
        CALL SetTL          ;17+57
        ;81
        
        LD A,$48            ;7  OP2.TL
        CALL SetTL          ;17+57
        ;81
        
        
        EXX                 ;4
        EX AF,AF'           ;4
        RET                 ;10
        ;18(313=72+50+11+81+81+18=)



Pcm2ch:
        EX AF,AF'           ;4
        EXX                 ;4
        
        LD A,$FF            ;7
        OUT ($E4),A         ;11
        
        LD A,$27            ;7
        OUT ($44),A         ;11
        JP @F               ;10
@:      LD A,%00010101      ;7
        OUT ($45),A         ;11
        ;72
        
        
BufAdr: LD A,(PcmBuf)       ;13
        LD HL,BufAdr+1      ;10
        INC (HL)            ;11
        
        LD H,TL>>8          ;7
        LD L,A              ;4
        
        LD A,$40            ;7  OP1.TL
        CALL SetTL          ;17+57
        
        LD A,$48            ;7  OP2.TL
        CALL SetTL          ;17+57
        ;207(279)
        
        
Switch: JR Get              ;12 LD A,n;7
        ;12|7(291|286)
        
        
Put:    ;286
        DI                  ;4
        LD A,2<<5           ;7  Cmd.Put
        OUT ($FE),A         ;11
        ;22
        
        LD C,$FC            ;7
        ;7(25)
        
PcmAdrA:LD HL,0             ;10
        OUTI                ;16 Dat.Put (33)
        LD A,(HL)           ;7
        OUT ($FD),A         ;11 Dat.Put
        OR A                ;4
        JR Z,@F             ;7|12
        INC L               ;4
        LD (PcmAdrA+1),HL   ;16
@:      ;75|60
        
        LD A,1<<5           ;7  Cmd.Nop
        OUT ($FE),A         ;11
        ;18
        
PcmAdrB:LD HL,0             ;10
        OUTI                ;16 Dat.Put (26)
        LD A,(HL)           ;7
        OUT ($FD),A         ;11 Dat.Put
        OR A                ;4
        JR Z,@F             ;7|12
        INC L               ;4
        LD (PcmAdrB+1),HL   ;16
@:      ;75|60
        
        EI                  ;4
        LD A,Code_JR        ;7
        LD (Switch),A       ;13
        ;24
        
        EXX                 ;4
        EX AF,AF'           ;4
        RET                 ;10
        ;18(525=286+22+7+75+18+75+24+18=)
        
        
Get:    ;291
        DI                  ;4
        LD A,3<<5           ;7  Cmd.Get
        OUT ($FE),A         ;11
        ;22
        
        ;Wait.16-1 + 18
        LD HL,(BufAdr+1)    ;16
        LD A,1<<5           ;7  Cmd.Nop
        OUT ($FE),A         ;11
        ;34
        
        IN A,($FD)          ;11 Dat.Get
        LD (HL),A           ;7
        INC L               ;4
        IN A,($FC)          ;11 Dat.Get
        LD (HL),A           ;7
        ;40
        
        EI                  ;4
        LD A,Code_LD_A      ;7
        LD (Switch),A       ;13
        ;24
        
        EXX                 ;4
        EX AF,AF'           ;4
        RET                 ;10
        ;18(429=291+22+34+40+24+18=)



SetTL:
        OUT ($44),A         ;11
        JP @F               ;10
@:      LD A,(HL)           ;7
        OUT ($45),A         ;11
        INC H               ;4
        NOP                 ;4
        RET                 ;10
        ;57



IntVSync:
        PUSH AF             ;11
        PUSH HL             ;11
        
        LD A,$FF            ;7
        OUT ($E4),A         ;11
        
        LD HL,Run           ;10
        INC (HL)            ;7
        
        POP HL              ;10
        POP AF              ;10
        RET                 ;10
        ;87



PlayBgm:
        PUSH AF             ;11
        PUSH BC             ;11
        PUSH DE             ;11
        PUSH HL             ;11
        PUSH IX             ;15
        PUSH IY             ;15
        EX AF,AF'           ;4
        EXX                 ;4
        PUSH AF             ;11
        PUSH BC             ;11
        PUSH DE             ;11
        PUSH HL             ;11
        
        CALL Init           ;17
        
Polling:
        IN A,($9)           ;11
        RLCA                ;4  Cy = !StopKey
        JR NC,@F            ;7|12
        
        XOR A               ;4  A = 0
        LD HL,Run           ;10
        OR (HL)             ;7
        JR Z,Polling        ;7|12
        
        DEC (HL)            ;11
        CALL Sequence       ;17 todo
        CALL Send           ;17
        JP Polling          ;10
@:      
        
        CALL Term           ;17
        
        POP HL              ;10
        POP DE              ;10
        POP BC              ;10
        POP AF              ;10
        EXX                 ;4
        EX AF,AF'           ;4
        POP IY              ;14
        POP IX              ;14
        POP HL              ;10
        POP DE              ;10
        POP BC              ;10
        POP AF              ;10
        RET                 ;10
        ;126



Send:
        LD C,$44            ;7
        LD HL,SendBuf       ;10
        ;17
        
FmNum:  LD A,0              ;7
        ADD A,A             ;4
        JR Z,FmSkip         ;7|12
        LD B,A              ;4
        ;22
@:      DI                  ;4
        OUTI                ;16
        INC C               ;4
        OUTI                ;16
        DEC C               ;4
        EI                  ;4
        DJNZ @B             ;8|13
FmSkip: ;81
        
SsgNum: LD A,0              ;7
        ADD A,A             ;4
        JR Z,SsgSkip        ;7|12
        LD B,A              ;4
        ;22
@:      DI                  ;4
        OUTI                ;16
        INC C               ;4
        OUTI                ;16
        DEC C               ;4
        EI                  ;4
        DJNZ @B             ;8|13
SsgSkip:;61
        
        RET                 ;10


SendBuf:
        DS $200             ;

Run:
        DB 0                ;
