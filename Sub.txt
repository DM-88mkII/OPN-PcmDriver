


Cmd:    align $100
Cmd0:   JP Exit             ;10 Return BIOS
        align $4
Cmd1:   JP Nop              ;10
        align $4
Cmd2:   JP Put              ;10
        align $4
Cmd3:   JP Get              ;10

Decomp:
        align $100          ;
        DS $100             ;H
        DS $100             ;L

LzcP:
        align $100          ;
        DS $100             ;n<<3

LzcM:
        align $100          ;
        DS $100             ;n<<3

CompP:
        align $100          ;
        DS $100             ;H
        DS $100             ;L

CompM:
        align $100          ;
        DS $100             ;H
        DS $100             ;L



PcmMode:
        DI                  ;4
        PUSH AF             ;11
        PUSH BC             ;11
        PUSH DE             ;11
        PUSH HL             ;11
        IN A,($FE)          ;11
        PUSH AF             ;11
        
        
        LD A,%01110000      ;7  Signal.on   %0111....
        OUT ($FE),A         ;11
@:      
        IN A,($FE)          ;11
        AND $0F             ;7
        CP 1<<2             ;7  Cmd.Nop
        JR NZ,@B            ;7|12
        
        XOR A               ;4  Signal.off  %0000....
        OUT ($FE),A         ;12
@:      
        IN A,($FE)          ;11
        CP 1<<2             ;7  Cmd.Nop
        JR NZ,@B            ;7|12
        
Polling:
        LD H,Cmd>>8         ;7
        LD C,$FE            ;7
Nop:    
        IN L,(C)            ;12
        JP (HL)             ;4+10
        ;26+26-1
        
Exit:   
        POP AF              ;10
        OUT ($FE),A         ;11
        POP HL              ;10
        POP DE              ;10
        POP BC              ;10
        POP AF              ;10
        EI                  ;4
        RET                 ;10



Put:    ;26+26-1
        ;Wait.(35-26=9)
        LD A,I              ;9
        ;9
        
        IN A,($FD)          ;11 Get.Dat
        LD (PcmA1+1),A      ;13
        IN A,($FC)          ;11 Get.Dat
        LD (PcmA2+1),A      ;13
        JP @F               ;10
@:      JP @F               ;10
@:      LD A,0              ;7
        ;75
        
        ;Wait.18 + 28
        JP @F               ;10
@:      JP @F               ;10
@:      JP @F               ;10
@:      RLC A               ;8
        RLC A               ;8
        ;46
        
        IN A,($FD)          ;11 Get.Dat
        LD (PcmB1+1),A      ;13
        IN A,($FC)          ;11 Get.Dat
        LD (PcmB2+1),A      ;13
        ;48(9+75+46+48=178)
        
        
PcmA1:  LD B,0              ;7
PcmB1:  LD C,0              ;7
        CALL AddPcm         ;17
        LD (PcmX1q+1),A     ;13
        JP Polling          ;10



Get:    ;26+26-1
PcmX1:  LD A,0              ;7
        OUT ($FC),A         ;11 Dat.Put (18)
PcmX2:  LD A,0              ;7
        OUT ($FD),A         ;11 Dat.Put
        ;36
        
        
PcmX1q: LD A,0              ;7
        LD (PcmX1+1),A      ;13
        
PcmA2:  LD B,0              ;7
PcmB2:  LD C,0              ;7
        CALL AddPcm         ;17
        LD (PcmX2+1),A      ;13
        JP Polling          ;10



;       Input
;           B PcmA
;           C PcmB
;       Output
;           A PcmX = PcmA + PcmB
AddPcm:
        LD H,Decomp>>8      ;7
        LD L,B              ;4
        LD D,(HL)           ;7
        INC H               ;4
        LD E,(HL)           ;7  DE = PcmA16
        ;29
        
        DEC H               ;4
        LD L,C              ;4
        LD B,(HL)           ;7
        INC H               ;4
        LD C,(HL)           ;7  BC = PcmB16
        ;26
        
        EX DE,HL            ;4  HL = PcmA16
        OR A                ;4  Cf = 0
        ADC HL,BC           ;15 HL = PcmX16 = PcmA16 + PcmB16
        EX DE,HL            ;4  DE = PcmX16
        JP M,Minus          ;10
        ;37(92)
        
Plus:   
        LD H,LzcP>>8        ;7
        LD A,D              ;4
        AND A               ;4
        JR NZ,LzcP_H        ;7|12
LzcP_L: LD L,E              ;4  A = 0<<3
        JP @F               ;10
LzcP_H: LD A,8<<3           ;7
        LD L,D              ;4
@:      ADD A,(HL)          ;7
        LD C,A              ;4  C = Exponent
        ;47|49
        
        LD H,CompP>>8       ;7  Sign.0 | Frac
        LD L,D              ;4
        LD A,(HL)           ;7
        INC H               ;4
        LD L,E              ;4
        OR (HL)             ;7
        OR C                ;4
        RET                 ;10
        ;47(186|188)
        
Minus:  
        LD H,LzcM>>8        ;7
        LD A,D              ;4
        INC A               ;4
        JR NZ,LzcM_H        ;7|12
LzcM_L: LD L,E              ;4  A = 0<<3
        JP @F               ;10
LzcM_H: LD A,8<<3           ;7
        LD L,D              ;4
@:      ADD A,(HL)          ;7
        LD C,A              ;4  C = Exponent
        ;47|49
        
        LD H,CompM>>8       ;7  Sign.1 | Frac
        LD L,D              ;4
        LD A,(HL)           ;7
        INC H               ;4
        LD L,E              ;4
        OR (HL)             ;7
        OR C                ;7
        RET                 ;10
        ;47(186|188)
